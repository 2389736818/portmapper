apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'org.chris.portmapper.PortMapperStarter'
version = '1.9.6'
sourceCompatibility = 1.7

sourceSets {
	main {
		java {
			srcDir 'src'
			srcDir 'src_weupnp'
		}
		resources { srcDir 'resources' }
	}
	test {
		java { srcDir 'test' }
	}
}

[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

jar {
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest { attributes 'Main-Class': mainClassName }
}

repositories {
	flatDir { dirs 'lib' }
	mavenCentral()
	maven {
		url 'http://teleal.org/m2'
	}
}

dependencies {
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-core:1.9.5'
	compile 'commons-cli:commons-cli:1.2'
	compile 'org.slf4j:slf4j-api:1.7.6'
	compile 'ch.qos.logback:logback-classic:1.1.1'
	compile 'com.miglayout:miglayout:3.7.4'
	compile 'org.jdesktop.bsaf:bsaf:1.9.2'
	compile ':sbbi-upnplib:1.0.4'
	runtime 'commons-jxpath:commons-jxpath:1.1' // sbbi
	compile 'org.teleal.cling:cling-core:1.0.5'
	compile 'org.teleal.cling:cling-support:1.0.5'
}